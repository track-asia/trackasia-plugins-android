apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: '../gradle/artifact-settings.gradle'
apply from: '../gradle/publish-root.gradle'

version = project.ext.versionName
group = project.ext.mapLibreArtifactGroupId

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.sourceFiles
  classpath = files(android.bootClasspath)
  failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'dokka'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                            
                groupId this.group
                artifactId project.ext.mapLibreArtifactId
                
                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar  

                version this.version

                pom {
                    name = project.ext.mapLibreArtifactTitle
                    description = project.ext.mapLibreArtifactTitle
                    url = project.ext.mapLibreArtifactUrl
                    licenses {
                        license {
                            name = project.ext.mapLibreArtifactLicenseName
                            url = project.ext.mapLibreArtifactLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = project.ext.mapLibreDeveloperId
                            name = project.ext.mapLibreDeveloperName
                            email = 'maplibre@maplibre.org'
                        }
                    }
                    scm {
                        connection = project.ext.mapLibreArtifactScmUrl
                        developerConnection = project.ext.mapLibreArtifactScmUrl
                        url = project.ext.mapLibreArtifactUrl
                    }
                }            
            }
        }
    }
}


// See: https://github.com/chrisbanes/gradle-mvn-push/issues/43#issuecomment-84140513
afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
            classpath += configurations.javadocDeps
        }
    }
}

signing {
    sign publishing.publications
}
