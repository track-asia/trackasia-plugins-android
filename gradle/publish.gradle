apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: '../gradle/artifact-settings.gradle'
apply from: '../gradle/publish-root.gradle'

version = project.ext.versionName
group = project.ext.trackasiaArtifactGroupId

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.sourceFiles
  classpath = files(android.bootClasspath)
  failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'dokka'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                            
                groupId this.group
                artifactId project.ext.trackasiaArtifactId
                
                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar  

                version this.version

                pom {
                    name = project.ext.trackasiaArtifactTitle
                    description = project.ext.trackasiaArtifactTitle
                    url = project.ext.trackasiaArtifactUrl
                    licenses {
                        license {
                            name = project.ext.trackasiaArtifactLicenseName
                            url = project.ext.trackasiaArtifactLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = project.ext.trackasiaDeveloperId
                            name = project.ext.trackasiaDeveloperName
                            email = 'trackasia@track-asia.com'
                        }
                    }
                    scm {
                        connection = project.ext.trackasiaArtifactScmUrl
                        developerConnection = project.ext.trackasiaArtifactScmUrl
                        url = project.ext.trackasiaArtifactUrl
                    }
                }            
            }
        }
    }
}


// See: https://github.com/chrisbanes/gradle-mvn-push/issues/43#issuecomment-84140513
afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
            classpath += configurations.javadocDeps
        }
    }
}

signing {
    sign publishing.publications
}
